grammar org.uqbar.project.wollok.WollokDsl hidden(WS, ML_COMMENT, SL_COMMENT)

generate wollokDsl "http://uqbar-project.org/wollok/WollokDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


WFile:
    (imports+=Import)*
	elements+=WLibraryElement* 
	(main=WProgram | tests+=WTest+ | suites+=WSuite+)?
;

// IMPORTS

Import: 'import' importedNamespace=QualifiedNameWithWildcard;
QualifiedName: ID ('.' ID)*;
QualifiedNameWithWildcard: QualifiedName ('.' '*')?;

// CONTENT


WProgram:
	'program' name=ID '{'
		(elements+=WExpressionOrVarDeclaration ';'?)*
	'}'
;

WTest:
	'test' name=STRING '{'
		(elements+=WExpressionOrVarDeclaration ';'?)*		
	'}'
;

WSuite:
	'describe' name=STRING 
	'{'
		(members+=WVariableDeclaration ';'?)*
		(fixture=WFixture)?
		(members+=WMethodDeclaration ';'?)*
		(tests+=WTest)*
	'}'
;

WFixture:
	{WFixture}
	'fixture' '{'
		(elements+=WExpression ';'?)*
	'}'	
;

WPackage:
	'package' name=QualifiedName '{'
		elements+=(WClass | WNamedObject)*
	'}'
;

// reglas generales (solo para ordenar y para fijar bien las relaciones entre clases porque a veces xtext no genera bien.
WLibraryElement: (WPackage | WClass | WNamedObject | WMixin | WVariableDeclaration);
WNamed: WMethodDeclaration | WReferenciable | WPackage | WClass | WMixin;
WMethodContainer: WClass | {WObjectLiteral} | WNamedObject | WMixin | WSuite;
WReferenciable: WParameter | {WVariable} | WNamedObject ;

WObjectLiteral returns WExpression:
	{WObjectLiteral}
	=>'object'
	('inherits' parent=[WClass|QualifiedName] 
		(=> ( '(' parentParameters = WArgumentList ')'))?
	)?
	('mixed with' mixins+=[WMixin|QualifiedName] (('and'|',') mixins+=[WMixin|QualifiedName])*)?
	'{'
		(members+=WVariableDeclaration ';'?)*
		(members+=WMethodDeclaration ';'?)*
	'}'
;

WVariableDeclaration returns WExpression:
	{WVariableDeclaration}
	(writeable?='var'|'const') (property?='property')? variable=WVariable ('=' right=WExpression)?
;

WMethodDeclaration:
	(overrides?='override')? 'method'
		name=WMethodName '(' (parameters+=WParameter (',' parameters+=WParameter)* )?')' 
		(native?='native')? 
		
		((expression=WBlockExpression) | (expressionReturns?='=' expression=WOrExpression))?
;

WMethodName:
	// TODO: 2 esto deberia tener un @check el mÃ©todo con +, o - necesita tener un solo argumento
	ID | OpAdd | OpMulti | OpEquality | OpOther | OpCompare | OpAnd | OpOr | OpPower
;

WParameter:
	name=ID (varArg?='...')?
;

WVariable:
	name=ID 
;

	
// **************************
// ** lexer rules
// **************************

Number hidden():
	HEX | (INT | DECIMAL) ('.' (INT | DECIMAL))?;
	
terminal HEX:
	('0x'|'0X') ('0'..'9'|'a'..'f'|'A'..'F'|'_')+ 
	('#' (('b'|'B')('i'|'I') | ('l'|'L')))?;
	
terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9'|'_')*; 

terminal DECIMAL:
	INT 
	(('e'|'E') ('+'|'-')? INT)? 
	(('b'|'B')('i'|'I'|'d'|'D') | ('l'|'L'|'d'|'D'|'f'|'F'))?;

	
// ***********************
// ** as XBase
// ***********************

WExpression returns WExpression:
	WAssignment;
	
WAssignment returns WExpression :
	{WAssignment} feature=WVariableReference OpSingleAssign value=WAssignment
	| WOrExpression (
		=>({WBinaryOperation.leftOperand=current} feature=OpMultiAssign) rightOperand=WAssignment
	)?
;
	
OpSingleAssign:
	'='
;

OpMultiAssign:
	'+=' | '-=' | '*=' | '/=' | '%=' | 
	'<' '<' '=' | 
	'>' '>'? '>=';
	
WOrExpression returns WExpression:
	WAndExpression (=>({WBinaryOperation.leftOperand=current} feature=OpOr) rightOperand=WAndExpression)*;
	
OpOr:
	'||' | 'or';

WAndExpression returns WExpression:
	WEqualityExpression (=>({WBinaryOperation.leftOperand=current} feature=OpAnd) rightOperand=WEqualityExpression)*;

OpAnd:
	'&&' | 'and';

WEqualityExpression returns WExpression:
	WRelationalExpression (=>({WBinaryOperation.leftOperand=current} feature=OpEquality) rightOperand=WRelationalExpression)*;

OpEquality:
	'==' | '!=' | '===' | '!==';

WRelationalExpression returns WExpression:
	WOtherOperatorExpression
	( //=>({WInstanceOfExpression.expression=current} 'instanceof') type=JvmTypeReference |
	 =>({WBinaryOperation.leftOperand=current} feature=OpCompare) rightOperand=WOtherOperatorExpression)*;

OpCompare:
	'>=' | '<' '=' | '>' | '<' ;

WOtherOperatorExpression returns WExpression:
	WAdditiveExpression (=>({WBinaryOperation.leftOperand=current} feature=OpOther)
	rightOperand=WAdditiveExpression)*;

OpOther:
	  '..<'
	| '>' '..'
	| '..'
	| '->' 
	| '>' (=>('>' '>') | '>') 
	| '<' (=>('<' '<') | '<' | '=>')
	| '<>'
	| '?:';

WAdditiveExpression returns WExpression:
	WMultiplicativeExpression (=>({WBinaryOperation.leftOperand=current} feature=OpAdd)
	rightOperand=WMultiplicativeExpression)*;

OpAdd:
	'+' | '-';
 
WMultiplicativeExpression returns WExpression:
	WExponentiativeExpression (=>({WBinaryOperation.leftOperand=current} feature=OpMulti) rightOperand=WExponentiativeExpression)*; 

	
OpMulti:
	'*' | '/' | '%';

WExponentiativeExpression returns WExpression:
	WUnaryOperation (=>({WBinaryOperation.leftOperand=current} feature=OpPower) rightOperand=WExponentiativeExpression)?;

OpPower:
	'**';

WUnaryOperation returns WExpression:
	{WUnaryOperation} feature=OpUnary operand=WUnaryOperation
	| WPostfixOperation 
;

OpUnary:
	'not' | "!" | "-" | "+";
	
WPostfixOperation returns WExpression:
	WMemberFeatureCall =>({WPostfixOperation.operand=current} feature=OpPostfix)?
;

OpPostfix:
	"++" | "--"
;

WFeatureCall : WSuperInvocation | WMemberFeatureCall;

WMemberFeatureCall returns WExpression:
	WPrimaryExpression
	(
		=>({WMemberFeatureCall.memberCallTarget=current} ("."| nullSafe?="?.")) 
		=>feature=FeatureCallID 
		(('(' 
			(memberCallArguments+=WExpression (',' memberCallArguments+=WExpression)*)? 
		')') | memberCallArguments+=WClosure	)
	)*;

FeatureCallID:
	ID;

WSelf returns WExpression:
	{WSelf} 'self'
;

WPrimaryExpression returns WExpression:
	WVariableReference |
	WLiteral |
	WConstructorCall |
	WParenthesizedExpression |
	WSuperInvocation |
	WIfExpression |
	WTry |
	WReturnExpression | 
	WThrow
//	XForLoopExpression |
;

WSuperInvocation returns WExpression: 
	{WSuperInvocation} 
	'super' 
	('(' 
		(memberCallArguments+=WExpression (',' memberCallArguments+=WExpression)*)? 
	')')	
;

WParenthesizedExpression returns WExpression:
	'(' WExpression ')';


WBlockExpression returns WExpression: 
	{WBlockExpression}
	'{'
		(expressions+=WExpressionOrVarDeclaration ';'?)*
	'}';

WExpressionOrVarDeclaration returns WExpression:
	WVariableDeclaration | WExpression;

WVariableReference:
	{WVariableReference} ref=[WReferenciable]
;

WBlockOrExpression returns WExpression:
	(=> WBlockExpression) | WExpression;

WIfExpression:
	'if' '(' condition=WExpression ')' 
		then=WBlockOrExpression
	(=>'else'
		else=WBlockOrExpression
	)?
;

// LITERALS

WLiteral returns WExpression:
	WClosure |
	WBooleanLiteral |
	WNumberLiteral |
	WNullLiteral |
	WStringLiteral |
	WObjectLiteral |
	WSelf |
	WCollectionLiteral
;

WBooleanLiteral returns WExpression :
	{WBooleanLiteral} ('false' | isTrue?='true');

WNullLiteral returns WExpression :
	{WNullLiteral} 'null';

WStringLiteral returns WExpression:
	{WStringLiteral} value=STRING;

WNumberLiteral returns WExpression :
	{WNumberLiteral} value=Number;

// CLOSURES

WClosure returns WExpression:
	=>({WClosure} 
	'{') 
		=>((parameters+=WParameter (',' parameters+=WParameter)*)? '=>')?
		expression=WBlockInClosure 
	'}';

WBlockInClosure returns WExpression: 
	{WBlockExpression}
	(expressions+=WExpressionOrVarDeclaration ';'?)*;

// COLLECTIONS

WCollectionLiteral:
	WListLiteral | WSetLiteral;

WListLiteral:
	{WListLiteral} '[' (elements+=WExpression (',' elements+=WExpression )*)? ']';

WSetLiteral:
	{WSetLiteral} '#{' (elements+=WExpression (',' elements+=WExpression )*)? '}';
	
WNamedObject:
	'object' name=ID 
		('inherits' parent=[WClass|QualifiedName] 
			(=> ( '(' parentParameters = WArgumentList ')'))?
		)?
		('mixed with' mixins+=[WMixin|QualifiedName] (('and'|',') mixins+=[WMixin|QualifiedName])*)?
	'{'
		(members+=WVariableDeclaration ';'?)*
		(members+=WMethodDeclaration ';'?)*
	'}'
;

WCanExtendClass:
	WClass | WNamedObject
;

// CLASSES

WClass:
	'class' name=ID 
		('inherits' parent=[WClass|QualifiedName])? 
		('mixed with' mixins+=[WMixin|QualifiedName] (('and'|',') mixins+=[WMixin|QualifiedName])*)? 
	'{'
		(members+=WVariableDeclaration ';'?)*
		(constructors+=WConstructor)*
		(members+=WMethodDeclaration ';'?)*
	'}'
;

// MIXINS

WMixin:
	'mixin' name=ID '{'
		(members+=WVariableDeclaration ';'?)*
		(members+=WMethodDeclaration ';'?)*
	'}'
;

WMember:
	WVariableDeclaration | WMethodDeclaration
;

WConstructor:
	{WConstructor} 'constructor' '(' (parameters+=WParameter (',' parameters+=WParameter)*)? ')'
		('=' delegatingConstructorCall=WDelegatingConstructorCall)?
		(expression=WBlockExpression)?
;

WDelegatingConstructorCall:
	WSelfDelegatingConstructorCall | WSuperDelegatingConstructorCall
;

// somehow this should be references to WConstructor
WSelfDelegatingConstructorCall:
	{WSelfDelegatingConstructorCall} 'self' '(' 
	(=> argumentList=WPositionalArgumentsList)?
	')'
;

// somehow this should be references to WConstructor
WSuperDelegatingConstructorCall:
	{WSuperDelegatingConstructorCall} 'super' '(' 
	(=> argumentList=WPositionalArgumentsList)?
	')'
;

WConstructorCall:
	'new' classRef=[WClass|QualifiedName] '('
	(=> argumentList=WArgumentList)?
	')'
	('with' mixins+=[WMixin])*
;

WArgumentList: (=> WNamedArgumentsList | WPositionalArgumentsList);

WNamedArgumentsList:
	{WNamedArgumentsList}
	(=> initializers+=WInitializer (',' initializers+=WInitializer)*)
;

WPositionalArgumentsList:
	{WPositionalArgumentsList}
	(=> values += WExpression (',' values += WExpression)*)
;

WInitializer:
	initializer= WVariable "=" initialValue=WExpression
;

WTry:
	'try'
		expression=WBlockOrExpression
	(catchBlocks+=WCatch)*
	(=>'then always' alwaysExpression=WBlockOrExpression)?
;

WCatch:
	=>'catch' exceptionVarName=WVariable (':' exceptionType=[WClass|QualifiedName])?
		expression=WBlockOrExpression 
;

WReturnExpression:
	'return' expression=WExpression	
;

WThrow:
	'throw' exception=WExpression
;

// I take the definitions from Terminals grammar, because having a super language, makes Xtext to generate
// two injectors in the standalone process. The extra injector takes 18% of time in running a simple program.

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;
